apply plugin: 'kotlin'
apply plugin: 'kotlin2js'

def outDir = "${buildDir}/js"

repositories {
	jcenter()
	mavenCentral()
	maven { url "http://repository.jetbrains.com/all" }
}

configurations {
	ktlint
}

dependencies {
	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-js-library:$kotlin_version"

	testCompile 'junit:junit:4.12'
	testCompile 'com.nhaarman:mockito-kotlin:0.3.1'
	testCompile 'org.jetbrains.spek:spek:1.0.9'
	testCompile 'org.mockito:mockito-core:2.0.86-beta'

	ktlint 'com.github.shyiko:ktlint:0.1.2'
}

compileKotlin2Js {
    kotlinOptions.sourceMap = true
    kotlinOptions.outputFile = "${outDir}/redux-kotlin.js"
    kotlinOptions.suppressWarnings = true
    kotlinOptions.verbose = true
}

jar {
    from sourceSets.main.allSource
    include "**/*.kt"

    from outDir
    include "**/*.js"

    manifest {
        attributes(
                "Specification-Title": "Kotlin JavaScript Lib",
                "Kotlin-JS-Module-Name": "redux-kotlin"
        )
    }
}

task ktlint(type: JavaExec) {
	main = "com.github.shyiko.ktlint.Main"
	classpath = configurations.ktlint
	args "src/**/*.kt"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

check.dependsOn ktlint
jar.dependsOn(compileKotlin2Js)
